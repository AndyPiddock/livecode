#!
 
send "doPackage" to me
 
private function pathFromNative pPath
   if the platform is "win32" then
      replace backslash with slash in pPath
   end if
   return pPath
end pathFromNative

on doPackage
   local tArgs
   put the commandArguments into tArgs
   
   local tEngine
   put the commandName into tEngine
   
   local tPackagerFilename
   put pathFromNative(the filename of me) into tPackagerFilename
   
   set the itemdelimiter to slash
   put "extensions" & slash & \
      "script-libraries" & slash & \
	  "extension-utils" & slash & \
      "extension-utils.livecodescript" \
      into item -2 to -1 of tPackagerFilename

   write tPackagerFilename & return to stdout
   start using stack tPackagerFilename

   local tSendCmd   
   switch tArgs[2]
   case "buildlcbextensions"
      -- Resolve dependency order of list of extensions
      -- and build each one.
      put "buildPackageAndExtractLCBExtensions" into tSendCmd
      break
   case "extractdocs"
      put "extractDocs" into tSendCmd
      break
   case "buildextension"
      put "buildPackageAndExtractExtension" into tSendCmd
      break 
   default
      write "invalid mode" && tArgs[2] to stderr
      quit 1
      break
   end switch

   repeat with x = 3 to the number of elements in tArgs
      put pathFromNative(tArgs[x]) into tArgs[x]
      put " tArgs[" & x & "]," after tSendCmd
   end repeat
   delete the last char of tSendCmd
   
   try
      send tSendCmd to stack tPackagerFilename
   catch tError
      write tError to stderr
      quit 1
   end try
end doPackage